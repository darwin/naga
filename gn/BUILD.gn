import("//v8/gni/snapshot_toolchain.gni")
import("//v8/gni/v8.gni")

declare_args() {
  stpyv8_verbose_build = false

  python_includes = string_split(getenv("STPYV8_PYTHON_INCLUDES"), " ")
  python_libs = string_split(getenv("STPYV8_PYTHON_LIBS"), " ")
  python_ldflags = string_split(getenv("STPYV8_PYTHON_LDFLAGS"), " ")
  python_abiflags = getenv("STPYV8_PYTHON_ABIFLAGS")

  stpyv8_enable_precompiled_headers = true
}

declare_args() {
  # should be true when running with debug build of Python
  # reason: https://github.com/pybind/pybind11/pull/1438
  # we retrieve STPYV8_PYTHON_ABIFLAGS via `python3-config --abiflags`
  #   "dm" means debug-multithreaded
  #   "m" means multithreaded (normal release build)
  stpyv8_using_debug_python = python_abiflags == "dm"
}

if (stpyv8_verbose_build) {
  print("using precompiled headers", stpyv8_enable_precompiled_headers)
  print("using python_includes", python_includes)
  print("using python_libs", python_libs)
  print("using python_ldflags", python_ldflags)
  print("using python_abiflags", python_abiflags)
}

stpyv8_source_files = [
  "Config.h",
  "Context.cpp",
  "Context.h",
  "Debug.cpp",
  "Debug.h",
  "Engine.cpp",
  "Engine.h",
  "ForwardDeclarations.h",
  "Isolate.cpp",
  "Isolate.h",
  "JSException.cpp",
  "JSException.h",
  "JSObject.cpp",
  "JSObject.h",
  "JSObjectNull.cpp",
  "JSObjectNull.h",
  "JSObjectUndefined.cpp",
  "JSObjectUndefined.h",
  "JSObjectArray.cpp",
  "JSObjectArray.h",
  "JSObjectFunction.cpp",
  "JSObjectFunction.h",
  "JSObjectCLJS.cpp",
  "JSObjectCLJS.h",
  "JSStackFrame.cpp",
  "JSStackFrame.h",
  "JSStackTrace.cpp",
  "JSStackTrace.h",
  "Locker.cpp",
  "Locker.h",
  "Module.cpp",
  "Module.h",
  "Platform.cpp",
  "Platform.h",
  "Printing.cpp",
  "Printing.h",
  "PythonAllowThreadsGuard.cpp",
  "PythonAllowThreadsGuard.h",
  "PythonExceptionGuard.cpp",
  "PythonExceptionGuard.h",
  "PythonDateTime.cpp",
  "PythonDateTime.h",
  "PythonGIL.cpp",
  "PythonGIL.h",
  "PythonObject.cpp",
  "PythonObject.h",
  "PythonUtils.cpp",
  "PythonUtils.h",
  "Script.cpp",
  "Script.h",
  "Tracer.cpp",
  "Tracer.h",
  "Unlocker.cpp",
  "Unlocker.h",
  "Utils.cpp",
  "Utils.h",
  "V8Utils.cpp",
  "V8Utils.h",
]

stpyv8_source_file_paths = []

foreach(source, stpyv8_source_files) {
  stpyv8_source_file_paths += [ "../src/$source" ]
}

config("stpyv8_precompiled_headers") {
  if (stpyv8_enable_precompiled_headers) {
    if (is_win) {
      # This is a string rather than a file GN knows about. It has to match
      # exactly what's in the /FI flag below, and what might appear in the
      # source code in quotes for an #include directive.
      precompiled_header = "src/_precompile.h"

      # This is a file that GN will compile with the above header. It will be
      # implicitly added to the sources (potentially multiple times, with one
      # variant for each language used in the target).
      precompiled_source = "../src/_precompile.cpp"

      # Force include the header.
      cflags = [ "/FI$precompiled_header" ]
    } else if (is_mac) {
      precompiled_source = "../src/_precompile.h"
    }
  }
}

config("stpyv8_compiler_flags") {
  cflags_cc = [
    "-Wno-c++98-compat-extra-semi",
    "-Wno-error=unused-variable",
    "-Wno-error=unused-function",

    # note CLion uses some unknown pragmas suppressing code hints
    "-Wno-unknown-pragmas",
    "-std=c++17",
  ]
}

config("stpyv8_python_compiler_flags") {
  cflags = python_includes

  if (stpyv8_using_debug_python) {
    if (stpyv8_verbose_build) {
      print("using debug python")
    }
    defines = [ "Py_DEBUG" ]
  }
}

config("stpyv8_python_linker_flags") {
  ldflags = python_ldflags
}

config("stpyv8_pybind11_compiler_flags") {
  include_dirs = [ "../vendor/pybind11/include" ]
}

source_set("stpyv8_src") {
  sources = stpyv8_source_file_paths

  # don't use the default configs that BUILDCONFIG applied to us.
  configs -= [
    "//build/config/compiler:no_exceptions",
    "//build/config/compiler:no_rtti",

    # TODO: this was causing issues with pybind11 in debug mode, revisit this
    # https://stackoverflow.com/questions/8685045/xcode-with-boost-linkerid-warning-about-visibility-settings
    "//build/config/gcc:symbol_visibility_hidden",

    #"//build/config/compiler:default_symbols",

    # TODO: google's code conventions are quite strict
    #       revisit this after we get rid of boost
    "//build/config/clang:find_bad_constructs",
  ]

  # add some of our own settings...
  configs += [
    ":stpyv8_precompiled_headers",
    ":stpyv8_python_compiler_flags",
    ":stpyv8_compiler_flags",
    ":stpyv8_pybind11_compiler_flags",
  ]

  # this is very important
  # this will ensure our build uses the same headers as V8 build
  # see https://github.com/area1/stpyv8/issues/9
  configs += [ "//v8:external_config" ]

  deps = [ "//v8:v8_headers" ]
}

shared_library("stpyv8") {
  output_extension = "so"

  # add some of our own settings...
  configs += [ ":stpyv8_python_linker_flags" ]

  deps = [
    ":stpyv8_src",
    "//v8:v8_libplatform",
    "//v8:v8_monolith",
  ]
}
